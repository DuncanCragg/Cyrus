{ UID: uid-c102-dd84-8284-c360
  Rules: uid-c265-091b-418c-c329
  is: gui editable
  title: "CAP Payments Demo"
  view: { logo: http://www.shaunthesheep.be/shaunthesheep/foto/500/thefarmer-1.jpg
          text: "Welcome to the Farmer Support Agency!"
          start: { input: button label: "Apply for farming support" }
          admin: uid-f1d2-4269-6821-9d7a
  }
}

{ UID: uid-c265-091b-418c-c329
  is: gui rule editable
  when: "form submitted create application"
  Alerted: { is: form }
  view: { applications: { form: @Alerted } !=> @. with
    { UID: new
      Rules: uid-f265-941d-a0b8-ca5d uid-1cc7-4c21-9036-4e01 uid-872c-b29e-03a1-7b61 uid-9ce9-1bdd-ec05-c8fa
      is: gui editable
      title: "CAP application"
      form: @Alerted
      view: {
        title: "CAP Application"
        user: style "You:" @Alerted:user
      }
    }
  }
}

{ UID: uid-f265-941d-a0b8-ca5d
  is: gui rule editable
  when: "application created create land item"
  Notifying: => @. with @form
  view: {
    land: # => style "Your Land:"
      { UID: new
        Rules: uid-0215-038b-7558-041a uid-51b1-5d09-ed2f-519c
        is: updatable land
        title: Holding
        update-template: uid-5c78-11ee-a3c7-ba89
        constants: uid-2433-fd91-25ad-2e9b
      }
  }
}

{ UID: uid-5c78-11ee-a3c7-ba89
  Rules: uid-0215-038b-7558-041a uid-5f28-9174-b47d-2a74 uid-4a47-2cb6-8e99-a178 uid-6419-0368-25da-f91e
  is: updatable land editable template
  title: "Land area template"
  usage:           { input: chooser   label: "Land Usage:" range: {
    annex-1: "Annex I Products" grazing-cultivation: "Maintaining for grazing/cultivation"
    perm-grass: "Permanent Grassland" perm-crops: "Permanent Crops"
    nurseries: Nurseries short-coppice: "Short-Rotation Coppice" fallow: Fallow }
  }
  entitlement:     { input: textfield label: "Entitlement Ref:" }
  active:          { input: checkbox  label: "Confirm Actively Farmed" }
  ineligible-area: { input: textfield label: "Ineligible Area (ha):" value: 0 }
  fc-total:        { input: textfield label: "Woodland Total (£):" value: 0 }
  ne-total:        { input: textfield label: "Stewardship Total (£):" value: 0 }
  update-template: uid-9da2-71f4-4a2f-f389
}

{ UID: uid-9da2-71f4-4a2f-f389
  Rules: uid-6d46-73a1-05c4-1703 uid-c619-afdd-0d5d-1a5d
  is: updatable land editable template
  title: "Land feature template"
  usage:       { input: chooser   label: "Land Usage:" range: {
      wcg-key-broad:     "WCG: Delivering key priorities (broadleaf)"
      wcg-key-conifer:   "WCG: Delivering key priorities (conifer)"
      wcg-other-broad:   "WCG: Delivering other priorities (broadleaf)"
      wcg-other-conifer: "WCG: Delivering other priorities (conifer)"
      hls-hd11:          "HLS: HD11 Restoration of traditional water meadows"
      hls-ho1:           "HLS: HO1 Maintenance of lowland heathland"
      hls-hk15:          "HLS: HK15 Maintenance of grassland for target features"
      hls-he10:          "HLS: HE10 Floristically enhanced grass buffer strips"
      permanent:           "Permanent (e.g. farmhouse)"
      temporary:           "Temporary (e.g. caravan)"
      river:               River
      pond:                Pond
      polytunnel-concrete: "Polytunnel on Concrete"
      other-woodland:      "Other Woodland"
      other-wall:          "Other Wall"
    }
  }
  ineligible:  { input: checkbox  label: BPS-Ineligible }
}

{ UID: uid-fc2b-1ecb-5e7f-99c5
  bp-info: { style: { is: style direction: horizontal proportions: 75% }
             text: "CAP Info:" link: http://ec.europa.eu/agriculture/cap-post-2013/index_en.htm }
  fc-info: { style: { is: style direction: horizontal proportions: 75% }
             text: "Forestry Info:" link: http://www.forestry.gov.uk/forestry/infd-6dcegu }
  ne-info: { style: { is: style direction: horizontal proportions: 75% }
             text: "Stewardship Info:" link: http://www.naturalengland.gov.uk/ourwork/farming/funding/es/default.aspx }
}

{ UID: uid-1cc7-4c21-9036-4e01
  is: gui rule editable
  when: "application created create payment summaries and item"
  view: {
    BP: # => style "Basic Payment (£):" 0
    FC: # => style "Woodland Payment (£):" 0
    NE: # => style "Stewardship Payment (£):" 0
  }
  payment: # =>
      { UID: new
        Rules: uid-e683-5ee2-a986-b93b uid-d958-bf42-c730-8406 uid-73ec-5ea1-9cf0-dfa5 uid-8bbc-4ffc-dc43-5903 uid-a2c3-59ac-d898-0fc5
        is: payment editable
        land: @view:land:2
      }
}

{ UID: uid-2433-fd91-25ad-2e9b
  is: look-up editable
  title: "Payments Look-up"
  bp-payment-per-ha: 100
  bp-minimum-payout: 200
  bp-maximum-payout: 300000
  wcg-key-broad:     4800
  wcg-key-conifer:   4200
  wcg-other-broad:   3800
  wcg-other-conifer: 3200
  hls-hd11:          350
  hls-ho1:           200
  hls-hk15:          130
  hls-he10:          485
}

{ UID: uid-872c-b29e-03a1-7b61
  is: gui rule editable
  when: "gui built set up its styles"
  view: { style:    # => { is: style colours: lightgreen }
          user: style => { is: style direction: horizontal proportions: 75% }
          land: style => { is: style direction: horizontal proportions: 75% }
          BP:   style => { is: style direction: horizontal proportions: 50% colours: lightblue }
          FC:   style => { is: style direction: horizontal proportions: 50% colours: lightblue }
          NE:   style => { is: style direction: horizontal proportions: 50% colours: lightblue }
  }
}

{ UID: uid-51b1-5d09-ed2f-519c
  is: land rule editable
  when: "land area changes update total"
  area: => + @list:area
}

{ UID: uid-0215-038b-7558-041a
  is: land rule editable
  when: "land item notified add to list"
  Alerted: { is: land within: @ }
  list: @Alerted !=> @. with @Alerted
}

{ UID: uid-5f28-9174-b47d-2a74
  is: land rule editable
  when: "ineligible land seen calculate unclaimable area"
  list: { area: * ineligible: true }
  ineligible-area: => + @=list:area
}

{ UID: uid-6d46-73a1-05c4-1703
  is: land rule editable
  when: "area used for woodland creation multiply area by payment per hectare for usage"
  area: number
  usage: /wcg-/
  ne-total: => 0
  fc-total: => @area * ( @within:within:constants select @usage )
}

{ UID: uid-c619-afdd-0d5d-1a5d
  is: land rule editable
  when: "area used for stewardship multiply area by payment per hectare for usage"
  area: number usage: /hls-/
  fc-total: => 0
  ne-total: => @area * ( @within:within:constants select @usage )
}

{ UID: uid-4a47-2cb6-8e99-a178
  is: land rule editable
  when: "totals available calculate land total"
  list: { fc-total: * }
  fc-total: => integer ( + @=list:fc-total )
}

{ UID: uid-6419-0368-25da-f91e
  is: land rule editable
  when: "totals available calculate land total"
  list: { ne-total: * }
  ne-total: => integer ( + @=list:ne-total )
}

{ UID: uid-e683-5ee2-a986-b93b
  is: payment rule editable
  when: "eligible BP land seen calculate claimable area and multiply by payment per hectare"
  land: { list: { area: * usage: * entitlement: * active: true } }
  bp-total: => ( ( + @=land:list:area ) - ( + @=land:list:ineligible-area ) ) * @land:constants:bp-payment-per-ha
}

{ UID: uid-d958-bf42-c730-8406
  is: payment rule editable
  when: "basic payment calculated if less than min set to zero"
  bp-total: < @land:constants:bp-minimum-payout => 0
}

{ UID: uid-73ec-5ea1-9cf0-dfa5
  is: payment rule editable
  when: "basic payment calculated set maximum"
  bp-total: > @land:constants:bp-maximum-payout => @land:constants:bp-maximum-payout
}

{ UID: uid-8bbc-4ffc-dc43-5903
  is: payment rule editable
  when: "totals available calculate grand total"
  land: { list: { fc-total: * } }
  fc-total: => + @=land:list:fc-total
}

{ UID: uid-a2c3-59ac-d898-0fc5
  is: payment rule editable
  when: "totals available calculate grand total"
  land: { list: { ne-total: * } }
  ne-total: => + @=land:list:ne-total
}

{ UID: uid-9ce9-1bdd-ec05-c8fa
  is: gui rule editable
  when: "payment updates show it"
  view: { BP: number => integer @payment:bp-total
          FC: number => integer @payment:fc-total
          NE: number => integer @payment:ne-total }
}

{ UID: uid-f1d2-4269-6821-9d7a
  is: link list
  title: "Business-Configurable Items"
  list:
    uid-5c78-11ee-a3c7-ba89
    uid-9da2-71f4-4a2f-f389
    uid-2433-fd91-25ad-2e9b
    uid-6d46-73a1-05c4-1703
    uid-e683-5ee2-a986-b93b
    uid-d958-bf42-c730-8406
    uid-73ec-5ea1-9cf0-dfa5
}

