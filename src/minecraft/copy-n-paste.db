{ UID: uid-9a97-8344-8977-3a94
  is: editable list
  title: "Copy and Paste"
  list: uid-10e9-562b-aa5c-9d31
}

{ UID: uid-10e9-562b-aa5c-9d31
  is: editable player rule
  when: "not got one, create duplicator"
  duplicator: # =>
    { UID: new
      Rules:
        uid-4273-38c2-7470-e378
        uid-1889-d5df-0987-aa5f
        uid-2c47-f295-a400-2400
        uid-58d1-a064-2630-852f
        uid-316b-2c75-26b8-6337
        uid-45c3-b80e-deea-e160
      is: editable minecraft copypaste structure
      world: @world
      scanning: { for: blocks }
      mappings: { sandstone: quartz-block glass-pane: ice }
    }
}

{ UID: uid-4273-38c2-7470-e378
  is: editable structure rule
  when: "pickaxe placing, save first point"
  within:holding: pickaxe-gold
  within:placing: number
  p1: => @within:placing:0..2
  p2: => @within:placing:0..2
}

{ UID: uid-1889-d5df-0987-aa5f
  is: editable structure rule
  when: "axe placing, save second point"
  within:holding: hatchet-gold
  within:placing: number
  p2: => @within:placing:0..2
}

{ UID: uid-2c47-f295-a400-2400
  is: editable structure rule
  when: "first and second points available, calculate scan pos and size"
  p1: number
  p2: number
  scanning: {
    position: => (min (@p1:0 @p2:0)) (min (@p1:1 @p2:1)) (min (@p1:2 @p2:2))
    size: => ((abs (@p1:0 - @p2:0)) + 1) ((abs (@p1:1 - @p2:1)) + 1) ((abs (@p1:2 - @p2:2)) + 1)
  }
  Notifying: => @. with @world
}

{ UID: uid-58d1-a064-2630-852f
  is: editable copypaste structure rule
  when: "scan results notified, capture link"
  Alerted: { is: minecraft structure }
  world-view: # => @Alerted
}

{ UID: uid-316b-2c75-26b8-6337
  is: editable structure rule
  when: "shovel placing, save copy point"
  within:holding: shovel-gold
  within:placing: number
  position: => @within:placing:0..2 + (0 1 0)
}

{ UID: uid-45c3-b80e-deea-e160
  is: editable copypaste structure rule
  when: "scan results available, create copy structure"
  world-view: *
  materials: => @world-view:materials each ( @mappings select @.. else @.. )
  trail: => true
}

