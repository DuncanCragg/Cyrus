{ UID: uid-1044-e6e9-125e-e35c
  is: gui
  title: "Cyrus Minecraft Tutorial"
  view:
    { is: style direction: horizontal }
    { view: open     item: uid-5a7a-16e9-508f-2f65 }
    { view: open raw item: uid-27c9-154b-c4bf-0453 }
    { view: open raw item: uid-3b90-6720-d64f-a87c }
}

{ UID: uid-5a7a-16e9-508f-2f65
  is: article list
  title: "Instructions"
  list: uid-3592-f387-6662-8d44
        uid-4ee8-9084-8ab0-6a2b
        uid-1a57-fa37-17a0-8672
        uid-6672-f066-5321-d37f
        uid-6e91-d824-6fea-ce5c
        uid-1f07-587f-92de-7e44
        uid-33a7-efb7-aced-d48b
}

{ UID: uid-3592-f387-6662-8d44
  is: article
  title: "Welcome to Cyrus for Minecraft Programming"
  text: "Cyrus lets you program Minecraft with rules."
        "On the right you'll see your first Minecraft rule."
        "It should look something like this:"
        "|["
        "{ is: editable minecraft"
        "  title: \"\""
        "  Rules: {"
        "  }"
        "  world: http://localhost:8084/o/uid-b1d8-d170-a3d9-e693.json"
        "}"
        "]|"
        "If you hit the link after 'world:', you'll be able to see the state of the world - time of day, raining or snowing."
        "If you then hit the link after 'player:', you'll be able to see your player coordinates in the world. Watch it change as you move around. Copy the three coordinates - you're going to need them in the next step. Hit refresh to get back to the top."
}

{ UID: uid-4ee8-9084-8ab0-6a2b
  is: article
  title: "Placing some blocks"
  text: "Now edit the rule - by hitting the text, apart from the links:"
        "|["
        "{ is: editable minecraft"
        "  title: \"\""
        "  Rules: {"
      "![    is: minecraft rule]!"
      "![    placing: => glass box ( 1 1 1 ) at ( x y z )]!"
      "![    Notifying: => @. with @world]!"
        " }"
        "  world: http://localhost:8084/o/uid-b1d8-d170-a3d9-e693.json"
        "}"
        "]|"
        "/[Note: you must set the coordinates 'x y z' to your own player coordinates, copied from the player: object.]/"
        "Press the 'Update' button below this text to make the change. If it doesn't seem to work, hit refresh."
        "This will place a glass box of size 1x1x1 at the coordinates given."
        "The line 'placing: => ..' in the rule updates the spell with a corresponding 'placing:', which you'll see."
        "The Notifying line is to tell the world about this spell."
        "Try other box sizes (like '5 4 7' - not too big!) and material names like 'sand-stone', 'stone', 'ore-gold', etc"
        "Note: Edit the rule, not the spell! The rule rewrites the spell. This is more powerful than editing directly, as we will see."
}

{ UID: uid-1a57-fa37-17a0-8672
  is: article
  title: "Glass trails"
  text: "This is a spell to leave a trail of glass everywhere you go."
        "You may have thought that it's a bit clumsy finding and setting the player coordinates ourselves. So let's get the spell to do it for us:"
        "|["
        "  Rules: {"
        "    is: minecraft rule"
      "![    placing: => glass box ( 1 1 1 ) at ( @world:player:position + ( 3 0 3 ))]!"
        "    Notifying: => @. with @world"
        "  }"
        "]|"
        "We take the player position, whatever it's set to or changes to, and add three blocks North and East, for placing."
}

{ UID: uid-6672-f066-5321-d37f
  is: article
  title: "Scanning the blocks in the world"
  text: "Now let's add a scanner to the spell, which tells the world we'd like to be updated on the blocks in some region. The region will be -50 down from where we stand:"
        "|["
        "  Rules: {"
        "    is: minecraft rule"
        "    placing: => glass box ( 1 1 1 ) at ( @world:player:position + ( 3 0 3 ))"
      "![    scanning: => blocks ( 2 3 4 ) at ( @world:player:position + ( 0 -50 0 ))]!"
        "    Notifying: => @. with @world"
        "  }"
        "]|"
        "If you jump the link after 'world', then the link there called 'scans', you'll see your results: as you move around, you'll see different blocks. Try to find some air underground, indicating a cave. Then dig down, to see if you find it."
        "Going to the world object to see our results is a bit inconvenient. Luckily the world tells the spell about them. The other end of 'Notifying' is 'Alerted':"
        "|["
      "![  {]!"
      "![    is: minecraft rule]!"
      "![    Alerted: { is: minecraft world-view }]!"
      "![    scan: # => @Alerted]!"
      "![  } ]!"
        "]|"
        "Add this rule after the one that's there - paste it after the closing brace '}'"
        "Now, when the world Notifies us of our scan results, we get Alerted, so we check it's a 'minecraft world-view' before setting 'scan' to link to it."
        "The '#' checks that scan hasn't already been set, which means this will only be run or triggered once."
        "Now you'll see the link 'scan:' right in your spell. Much more convenient."
}

{ UID: uid-6e91-d824-6fea-ce5c
  is: article
  title: "Scanning the entities in the world"
  text: "Looking for cows, zombies, users:"
        "|["
        "  Rules: {"
        "    is: minecraft rule"
      "![    scanning: => entities ( 20 3 20 ) at ( @world:player:position + ( -10 -1 -10 ))]!"
        "    Notifying: => @. with @world"
        "  }"
        "  {"
        "    is: minecraft rule"
        "    Alerted: { is: minecraft world-view }"
        "    scan: # => @Alerted"
        "  }"
        "]|"
}

{ UID: uid-1f07-587f-92de-7e44
  is: article
  title: "Alchemy"
  text: "Set 'casting' to 'true' in the following. It creates a glass box inside which stone is turned to gold!"
         uid-4d0b-47d9-151c-d366
}

{ UID: uid-33a7-efb7-aced-d48b
  is: article
  title: "Pyramid"
  text: "Set 'casting' to 'true' in the following. It creates a pyramid with various parameters you can adjust, for width, height, taper and materials."
         uid-a70f-cd0b-c257-a901
}

{ UID: uid-b1d8-d170-a3d9-e693
  Class: net.minecraft.src.MinecraftWorld
  is: queryable updatable 3d minecraft world
}

{ UID: uid-3b90-6720-d64f-a87c
  is: minecraft cow rule
  when: "position updates, set copy of it"
  cow-pos: => @position
}

{ UID: uid-27c9-154b-c4bf-0453
  is: editable minecraft
  title: ""
  Rules: {}
  world: uid-b1d8-d170-a3d9-e693
}

{ UID: uid-df7c-2043-f410-724f
  is: editable minecraft structure rule
  when: "scan blocks notified, save"
  Alerted: { is: minecraft world-view }
  scan-blocks: # => @Alerted
}

{ UID: uid-4d0b-47d9-151c-d366
  is: editable minecraft structure
  title: "Transform block types"
  Rules: uid-7348-7203-31f7-4a13 uid-df7c-2043-f410-724f uid-af69-1d57-8394-4157
  structure: alchemy
  casting: false
  world: uid-b1d8-d170-a3d9-e693
}

{ UID: uid-7348-7203-31f7-4a13
  is: editable minecraft structure rule
  when: "run, set scanning area"
  structure: alchemy
  casting: true => false
  world: { player: { position: * }}
  position: => @world:player:position + (3 1 0)
  scanning: => blocks ( 2 2 2 ) at @position
  scan-blocks: => #
  placing: => glass box ( 4 4 4 ) at (@position + (-1 -1 -1))
  Notifying: => @. with @world
}

{ UID: uid-af69-1d57-8394-4157
  is: editable minecraft structure rule
  when: "block list updates, if certain blocks seen, convert them"
  scan-blocks: *
  placing: => (@scan-blocks:list each ({ stone: ore-gold sand: glass dirt: ore-coal } select @.. else @..)) at @position
  Notifying: => @. with @world
}

{ UID: uid-a70f-cd0b-c257-a901
  is: editable minecraft structure
  title: "Build Pyramids"
  Rules: uid-320d-55ec-ca58-8f71 uid-77a8-38ec-e0bc-b5f1 uid-df7c-2043-f410-724f uid-4506-0c41-ac8e-4084 uid-f363-ddd9-4da2-4c73
  structure: pyramid
  casting: false
  world: uid-b1d8-d170-a3d9-e693
  width: 7
  height: 30
  taper: 1
  material: stone-double-slab glass brick stone
}

{ UID: uid-320d-55ec-ca58-8f71
  is: editable minecraft structure rule
  when: "pyramid constructor filled in, set up to build"
  structure: pyramid
  casting: true => false
  world: { player: { position: * }}
  position: => @world:player:position
  h: => 0
  w: => @width
  i: => 0
  m: => @material:0
}

{ UID: uid-77a8-38ec-e0bc-b5f1
  is: editable minecraft structure rule
  when: "run, set scanning area and notify world"
  w: *
  place-position: => @position + (( 8 - (@w / 2)) @h (8 - (@w / 2)))
  scan-position: => @place-position + ( 0 3 0 )
  scanning: => blocks ( @w 1 @w ) at @scan-position
  Notifying: => @. with @world
}

{ UID: uid-4506-0c41-ac8e-4084
  is: editable minecraft structure rule
  when: "no slab there, place it"
  scan-blocks: { position: @scan-position list: ((( !@m ))) }
  placing: => @m box ( @w 4 @w ) at @place-position
}

{ UID: uid-f363-ddd9-4da2-4c73
  is: editable minecraft structure rule
  when: "slab there, increase height and decrease width at current position"
  scan-blocks: { position: @scan-position list: not ((( !@m ))) }
  placing: => #
  h: <= @height => @h + 3
  w: > 0 => if (@h <= @height) then (@w - (2 * @taper)) else 0
  i: => @i + 1
  m: => @material select (@i % (count @material))
}

