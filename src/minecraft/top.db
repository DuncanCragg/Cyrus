{ UID: uid-1044-e6e9-125e-e35c
  is: gui
  title: "Cyrus Minecraft Tutorial"
  view:
    { is: style direction: horizontal }
    { view: open     item: uid-5a7a-16e9-508f-2f65 }
    { view: open raw item: uid-b1d8-d170-a3d9-e693 }
}

{ UID: uid-5a7a-16e9-508f-2f65
  is: article list
  title: "Instructions"
  list: uid-3592-f387-6662-8d44
        uid-4ee8-9084-8ab0-6a2b
        uid-6672-f066-5321-d37f
        uid-6e91-d824-6fea-ce5c
        uid-8c1a-79ee-15b5-94ac
        uid-bce6-6258-53cb-a39a
}

{ UID: uid-3592-f387-6662-8d44
  is: article
  title: "Welcome to Cyrus for Minecraft Programming"
  text: "Cyrus lets you program Minecraft mods with simple rules."
        "If you can write HTML or CSS or program formulae in Excel, you won't have any trouble programming Minecraft in Cyrus."
        "On the right you'll see a Cyrus 'object' representing the Minecraft world."
        "It should look something like this:"
        "|["
        "{ is: queryable updatable 3d minecraft world"
        "  global-rules: http://localhost:8084/o/uid-60b6-54d1-3b03-c265.json"
        "  player: http://localhost:8084/o/uid-e7ca-3831-5841-f8ea.json"
        "  time-stamp: 25215"
        "  time-of-day: 25550"
        "  daytime: true"
        "  raining: false"
        "  rain-strength: 0"
        "  thundering: false"
        "  seed: -5563498940576003772"
        "}"
        "]|"
        "When you run a Minecraft world, you'll be able to see the state of that world - time of day, raining or snowing."
        "If you then hit the link after 'player:', you'll be able to see your player coordinates in the world. Watch it change as you move around."
        "Note that, at any time, you can hit the browser refresh to get back to the top after jumping these links."
}

{ UID: uid-4ee8-9084-8ab0-6a2b
  is: article
  title: "Placing some blocks: a trail of glass"
  text: "Now you can edit your player object. Jump the player: link again and hit the text:"
        "This is a mod to leave a trail of glass everywhere you go."
        "|["
        "{ is: editable 3d minecraft player entity"
        "  name: \"..\""
        "  world: http://localhost:8084/o/uid-b1d8-d170-a3d9-e693.json"
        "  position: -638 63 -147"
      "![  Rules: {]!"
      "![    is: minecraft player rule]!"
      "![    placing: => { material: glass shape: box size: 1 1 1 position: (@position + (3 0 3)) }]!"
      "![    Notifying: => @. with @world]!"
        "  }"
        "}"
        "]|"
        "We take the player position, whatever it's set to or changes to, and add three blocks North and East, for placing."
        "Press the 'Update' button below this text to make the change. If it doesn't seem to work, hit refresh."
        "This will place a glass box of size 1x1x1 near the coordinates of your player."
        "The line 'placing: => ..' in the rule updates the object with a corresponding 'placing:', which you'll see."
        "The Notifying line is to tell the world about this object."
        "Try other box sizes (like '5 4 7' - not too big!) and material names like 'sand-stone', 'stone', 'ore-gold', etc."
        "Note: remember to edit the rule, not the object! The rule rewrites the object."
        "This is more powerful than editing directly, because you can get the position to keep updating for you, like this."
}

{ UID: uid-6672-f066-5321-d37f
  is: article
  title: "Scanning the blocks in the world"
  text: "Now let's add a scanner to the user, which tells the world we'd like to be updated on the blocks in some region."
        "The region will be -40 down from where we stand:"
        "|["
        "  Rules: {"
        "    is: minecraft player rule"
      "![    scanning: => { for: blocks size: 2 3 4 position: @position + (0 -40 0) }]!"
        "    Notifying: => @. with @world"
        "  }"
        "]|"
        "As you can see, scanning is similar to placing, except you're just looking, not changing."
        "You'll see your results at the 'world-view:' link: as you move around, you'll see different blocks."
        "Try to find some air underground, indicating a cave. Then dig down, to see if you find it."
        "The 'world-view:' link is set by another, global, rule."
        "To see the rules, hit refresh and then jump the 'global-rules:' link in the world object, then jump the first link."
        "A global rule is a rule applied to all objects, not a rule that was inside the object as before."
        "|["
        "  {"
        "    is: minecraft rule"
        "    when: \"world-view scan results notified, save\""
        "    Alerted: { is: minecraft world-view }"
        "    world-view: # => @Alerted"
        "  }"
        "]|"
        "The world tells the player or scanning object about the scan results: the other end of 'Notifying' is 'Alerted':"
        "When the world Notifies us of our scan results, we get Alerted, so we check it's a 'minecraft world-view' before setting 'world-view' to link to it."
        "The '#' checks that scan hasn't already been set, which means this will only be run or triggered once."
}

{ UID: uid-6e91-d824-6fea-ce5c
  is: article
  title: "Scanning the entities in the world"
  text: "Looking for cows, zombies, users:"
        "|["
        "  Rules: {"
        "    is: minecraft player rule"
      "![    scanning: => { for: entities size: 20 3 20 position: @position + (-10 -1 -10) }]!"
        "    Notifying: => @. with @world"
        "  }"
        "]|"
        "Changing 'blocks' to 'entities' returns the positions and links of users, cows, zombies, etc."
        "Spawn some zombies and skeletons and jump the 'world-view:' link to see them."
}

{ UID: uid-8c1a-79ee-15b5-94ac
  is: article
  title: "Dowsing the Cowpath Mod"
  text: "Spawn some cows, now. They should leave a trail of mainly glass behind whenever they 'dowse' something interesting below ground."
        "If it finds gold, it leaves a sand marker, if it finds iron, it leaves cobblestone, for coal it leaves wood."
        "Now find and jump a cow link. You should see something like:"
        "|["
        "{"
        "  is: editable 3d minecraft cow entity"
        "  name: Cow-9"
        "  world: http://localhost:8084/o/uid-b1d8-d170-a3d9-e693.json"
        "  scanning: { for: blocks size: 2 10 2 position: -552 18 -132 }"
        "  position: -552 68 -132"
        "  world-view: http://localhost:8084/o/uid-5696-300e-3ba7-e913.json"
        "  placing: ... at (-552 61 -132)"
        "}"
        "]|"
        "See all that scanning and placing it's doing? That's set by more global rules, just for cows this time."
        "Again, hit refresh and jump the 'global-rules:' link in the world object, then look at the second and third links:"
        "|["
        "{ is: editable minecraft cow rule"
        "  when: \"run, scan below ground\""
        "  scanning: => { for: blocks size: 2 10 2 position: @position + (0 -40 0) }"
        "  Notifying: => @. with @world"
        "}"
        "{ is: editable minecraft cow rule"
        "  when: \"something interesting found, set marker on surface\""
        "  world-view: { list: (((/ore-/))) }"
        "  placing: => { material: (@world-view:list each ({ ore-gold: sand ore-iron: cobblestone ore-coal: wood } select @.. else glass))"
        "                position: (@position + (0 -7 0))"
        "                trail: true"
        "  }"
        "}"
        "]|"
}

{ UID: uid-bce6-6258-53cb-a39a
  is: article
  title: "Structures"
  text: "Hit refresh, jump into your player, and type |[ my-box: uid-new ]|."
        "The my-box: property should now have a link: jump it, and enter this:"
        "|["
        "{ is: editable minecraft structure"
        "  world: http://localhost:8084/o/uid-b1d8-d170-a3d9-e693.json"
        "  Rules: {"
        "    is: structure rule"
        "    when: \"player moves, set structure position\""
        "    position:  => @world:player:position + (3 0 3)"
        "    Notifying: => @. with @world"
        "  }"
        "  material: stone-double-slab"
        "  shape: box"
        "  size: 7 4 7"
        "}"
        "]|"
        "You should see a stone-built box appear. If you move, it should track you around."
        "Change the size (not too big! 20 or less), and try different materials."
        "Try '+ (-3 0 -3)' to build around you, then material: air, to wipe out big chunks of territory!"
}

{ UID: uid-b1d8-d170-a3d9-e693
  Class: net.minecraft.src.MinecraftWorld
  is: queryable updatable 3d minecraft world
  global-rules: uid-60b6-54d1-3b03-c265
}

{ UID: uid-60b6-54d1-3b03-c265
  is: editable list
  title: "global rules"
  list: uid-df7c-2043-f410-724f
        uid-3b90-6720-d64f-a87c
        uid-adae-7060-6cb1-da1c
}

{ UID: uid-df7c-2043-f410-724f
  is: minecraft rule
  when: "world-view scan results notified, save"
  Alerted: { is: minecraft world-view }
  world-view: # => @Alerted
}

{ UID: uid-3b90-6720-d64f-a87c
  is: editable minecraft cow rule
  when: "run, scan below ground"
  scanning: => { for: blocks size: 2 10 2 position: @position + (0 -40 0) }
  Notifying: => @. with @world
}

{ UID: uid-adae-7060-6cb1-da1c
  is: editable minecraft cow rule
  when: "something interesting found, set marker on surface"
  world-view: { list: (((/ore-/))) }
  placing: => { material: (@world-view:list each ({ ore-gold: sand ore-iron: cobblestone ore-coal: wood } select @.. else glass))
                position: (@position + (0 -7 0))
                trail: true
  }
}

