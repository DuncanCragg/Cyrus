{ UID: uid-1044-e6e9-125e-e35c
  is: gui
  title: "Cyrus Minecraft Tutorial"
  view:
    { is: style direction: horizontal }
    { view: open     item: uid-5a7a-16e9-508f-2f65 }
    { view: open raw item: uid-b1d8-d170-a3d9-e693 }
}

{ UID: uid-5a7a-16e9-508f-2f65
  is: article list
  title: "Instructions"
  list: uid-3592-f387-6662-8d44
        uid-4ee8-9084-8ab0-6a2b
        uid-6672-f066-5321-d37f
        uid-6e91-d824-6fea-ce5c
        uid-8c1a-79ee-15b5-94ac
        uid-1f07-587f-92de-7e44
        uid-33a7-efb7-aced-d48b
}

{ UID: uid-3592-f387-6662-8d44
  is: article
  title: "Welcome to Cyrus for Minecraft Programming"
  text: "Cyrus lets you program Minecraft mods with simple rules."
        "If you can write HTML or CSS or program formulae in Excel, you won't have any trouble programming Minecraft in Cyrus."
        "On the right you'll see a Cyrus 'object' representing the Minecraft world."
        "It should look something like this:"
        "|["
        "{ is: queryable updatable 3d minecraft world"
        "  global-rules: http://localhost:8084/o/uid-60b6-54d1-3b03-c265.json"
        "  player: http://localhost:8084/o/uid-e7ca-3831-5841-f8ea.json"
        "  time-stamp: 25215"
        "  time-of-day: 25550"
        "  daytime: true"
        "  raining: false"
        "  rain-strength: 0"
        "  thundering: false"
        "  seed: -5563498940576003772"
        "}"
        "]|"
        "When you run a Minecraft world, you'll be able to see the state of that world - time of day, raining or snowing."
        "If you then hit the link after 'player:', you'll be able to see your player coordinates in the world. Watch it change as you move around."
        "Note that, at any time, you can hit the browser refresh to get back to the top after jumping these links."
}

{ UID: uid-4ee8-9084-8ab0-6a2b
  is: article
  title: "Placing some blocks: a trail of glass"
  text: "Now you can edit your player object. Jump the player: link again and hit the text:"
        "This is a mod to leave a trail of glass everywhere you go."
        "|["
        "{ is: editable 3d minecraft player entity"
        "  name: \"..\""
        "  world: http://localhost:8084/o/uid-b1d8-d170-a3d9-e693.json"
        "  position: -638 63 -147"
      "![  Rules: {]!"
      "![    is: minecraft player rule]!"
      "![    placing: => glass box (1 1 1) at (@position + (3 0 3))]!"
      "![    Notifying: => @. with @world]!"
        "  }"
        "}"
        "]|"
        "We take the player position, whatever it's set to or changes to, and add three blocks North and East, for placing."
        "Press the 'Update' button below this text to make the change. If it doesn't seem to work, hit refresh."
        "This will place a glass box of size 1x1x1 near the coordinates of your player."
        "The line 'placing: => ..' in the rule updates the object with a corresponding 'placing:', which you'll see."
        "The Notifying line is to tell the world about this object."
        "Try other box sizes (like '5 4 7' - not too big!) and material names like 'sand-stone', 'stone', 'ore-gold', etc"
        "Note: remember to edit the rule, not the object! The rule rewrites the object. This is more powerful than editing directly, as we will see."
}

{ UID: uid-6672-f066-5321-d37f
  is: article
  title: "Scanning the blocks in the world"
  text: "Now let's add a scanner to the user, which tells the world we'd like to be updated on the blocks in some region."
        "The region will be -50 down from where we stand:"
        "|["
        "  Rules: {"
        "    is: minecraft player rule"
      "![    scanning: => blocks (2 3 4) at (@position + (0 -50 0))]!"
        "    Notifying: => @. with @world"
        "  }"
        "A global rule is a rule applied to all objects, not a rule that was inside the object as before."
        "  {"
        "    is: minecraft rule"
        "    when: \"world-view scan results notified, save\""
        "    Alerted: { is: minecraft world-view }"
        "    world-view: # => @Alerted"
        "  }"
        "]|"
        "The world tells the player or scanning object about the scan results: the other end of 'Notifying' is 'Alerted':"
        "When the world Notifies us of our scan results, we get Alerted, so we check it's a 'minecraft world-view' before setting 'world-view' to link to it."
        "The '#' checks that scan hasn't already been set, which means this will only be run or triggered once."
        "You'll see your results at the 'world-view:' link: as you move around, you'll see different blocks."
        "Try to find some air underground, indicating a cave. Then dig down, to see if you find it."
}

{ UID: uid-6e91-d824-6fea-ce5c
  is: article
  title: "Scanning the entities in the world"
  text: "Looking for cows, zombies, users:"
        "|["
        "  Rules: {"
        "    is: minecraft player rule"
      "![    scanning: => entities (20 3 20) at (@position + (-10 -1 -10))]!"
        "    Notifying: => @. with @world"
        "  }"
        "]|"
        "Spawn some zombies and skeletons and jump the 'world-view:' link to see them."
}

{ UID: uid-8c1a-79ee-15b5-94ac
  is: article
  title: "Dowsing the Cowpath Mod"
  text: "Spawn some cows, then find and jump a cow link. You should see something like:"
        "|["
        "{"
        "  is: editable 3d minecraft cow entity"
        "  name: Cow-9"
        "  world: http://localhost:8084/o/uid-b1d8-d170-a3d9-e693.json"
        "  scanning: blocks (1 10 1) at (-552 18 -132)"
        "  position: -552 68 -132"
        "  world-view: http://localhost:8084/o/uid-5696-300e-3ba7-e913.json"
        "  placing: stone-double-slab box (1 1 1) at (-552 70 -132)"
        "}"
        "]|"
        "See all that scanning and placing it's doing? That's set by more global rules, just for cows this time."
        "To see the rules, hit refresh and then jump the 'global-rules:' link in the world object. Jump the second and third links:"
        "|["
        "{ is: editable minecraft cow rule"
        "  when: \"run, scan below ground\""
        "  scanning: => blocks (1 10 1) at (@position + (0 -50 0))"
        "  Notifying: => @. with @world"
        "}"
        "{ is: editable minecraft cow rule"
        "  when: \"something interesting found, set marker on surface\""
        "  world-view: { list: (((!stone))) }"
        "  placing: => stone-double-slab box (1 1 1) at (@position + (0 2 0))"
        "}"
        "]|"
        "The cow leaves a trail of paving slabs behind it whenever it 'dowses' something interesting below ground."
}

{ UID: uid-1f07-587f-92de-7e44
  is: article
  title: "Alchemy"
  text: "Set 'casting' to 'true' in the following. It creates a glass box inside which stone is turned to gold!"
         uid-4d0b-47d9-151c-d366
}

{ UID: uid-33a7-efb7-aced-d48b
  is: article
  title: "Pyramid"
  text: "Set 'casting' to 'true' in the following. It creates a pyramid with various parameters you can adjust, for width, height, taper and materials."
         uid-a70f-cd0b-c257-a901
}

{ UID: uid-b1d8-d170-a3d9-e693
  Class: net.minecraft.src.MinecraftWorld
  is: queryable updatable 3d minecraft world
  global-rules: uid-60b6-54d1-3b03-c265
}

{ UID: uid-60b6-54d1-3b03-c265
  is: editable list
  list: uid-df7c-2043-f410-724f
        uid-3b90-6720-d64f-a87c
        uid-adae-7060-6cb1-da1c
}

{ UID: uid-df7c-2043-f410-724f
  is: minecraft rule
  when: "world-view scan results notified, save"
  Alerted: { is: minecraft world-view }
  world-view: # => @Alerted
}

{ UID: uid-3b90-6720-d64f-a87c
  is: editable minecraft cow rule
  when: "run, scan below ground"
  scanning: => blocks (1 10 1) at (@position + (0 -50 0))
  Notifying: => @. with @world
}

{ UID: uid-adae-7060-6cb1-da1c
  is: editable minecraft cow rule
  when: "something interesting found, set marker on surface"
  world-view: { list: (((/ore-/))) }
  placing: => stone-double-slab box (1 1 1) at (@position + (0 2 0))
}

{ UID: uid-4d0b-47d9-151c-d366
  is: editable minecraft structure
  title: "Transform block types"
  Rules: uid-7348-7203-31f7-4a13 uid-af69-1d57-8394-4157
  structure: alchemy
  casting: false
  world: uid-b1d8-d170-a3d9-e693
}

{ UID: uid-7348-7203-31f7-4a13
  is: editable minecraft structure rule
  when: "run, set scanning area"
  structure: alchemy
  casting: true => false
  world: { player: { position: * }}
  position: => @world:player:position + (3 1 0)
  scanning: => blocks ( 2 2 2 ) at @position
  world-view: => #
  placing: => glass box ( 4 4 4 ) at (@position + (-1 -1 -1))
  Notifying: => @. with @world
}

{ UID: uid-af69-1d57-8394-4157
  is: editable minecraft structure rule
  when: "block list updates, if certain blocks seen, convert them"
  world-view: *
  placing: => (@world-view:list each ({ stone: ore-gold sand: glass dirt: ore-coal } select @.. else @..)) at @position
  Notifying: => @. with @world
}

{ UID: uid-a70f-cd0b-c257-a901
  is: editable minecraft structure
  title: "Build Pyramids"
  Rules: uid-320d-55ec-ca58-8f71 uid-77a8-38ec-e0bc-b5f1 uid-4506-0c41-ac8e-4084 uid-f363-ddd9-4da2-4c73
  structure: pyramid
  casting: false
  world: uid-b1d8-d170-a3d9-e693
  width: 7
  height: 30
  taper: 1
  material: stone-double-slab glass brick stone
}

{ UID: uid-320d-55ec-ca58-8f71
  is: editable minecraft structure rule
  when: "pyramid constructor filled in, set up to build"
  structure: pyramid
  casting: true => false
  world: { player: { position: * }}
  position: => @world:player:position
  h: => 0
  w: => @width
  i: => 0
  m: => @material:0
}

{ UID: uid-77a8-38ec-e0bc-b5f1
  is: editable minecraft structure rule
  when: "run, set scanning area and notify world"
  w: *
  place-position: => @position + (( 8 - (@w / 2)) @h (8 - (@w / 2)))
  scan-position: => @place-position + ( 0 3 0 )
  scanning: => blocks ( @w 1 @w ) at @scan-position
  Notifying: => @. with @world
}

{ UID: uid-4506-0c41-ac8e-4084
  is: editable minecraft structure rule
  when: "no slab there, place it"
  world-view: { position: @scan-position list: ((( !@m ))) }
  placing: => @m box ( @w 4 @w ) at @place-position
}

{ UID: uid-f363-ddd9-4da2-4c73
  is: editable minecraft structure rule
  when: "slab there, increase height and decrease width at current position"
  world-view: { position: @scan-position list: not ((( !@m ))) }
  placing: => #
  h: <= @height => @h + 3
  w: > 0 => if (@h <= @height) then (@w - (2 * @taper)) else 0
  i: => @i + 1
  m: => @material select (@i % (count @material))
}

