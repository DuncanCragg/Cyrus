{ UID: uid-b1d8-d170-a3d9-e693
  Class: net.minecraft.src.CyrusWorld
  is: queryable updatable 3d minecraft world
}

{ UID: uid-06b6-4631-166a-b013
  Class: net.minecraft.src.CyrusUser
  is: 3d minecraft player
}

{ UID: uid-fb97-b02c-e69b-90dd
  is: editable minecraft spell list
  list: uid-4d0b-47d9-151c-d366 uid-a70f-cd0b-c257-a901
}

{ UID: uid-4d0b-47d9-151c-d366
  is: editable minecraft spell
  title: "Cyrus Spell to lay out specific blocks"
  Rules: {
    is: minecraft spell rule not
    when: "run, set scanning box"
    player: { position: * }
    position: # => @player:position + (3 1 0)
    scanning: => box ( 2 2 2 ) at @position
    placing: => glass box ( 4 4 4 ) at (@position + (-1 -1 -1))
    Notifying: => @. with @world
  }{
    is: minecraft spell rule
    when: "block list in, save it, and if certain blocks seen, convert them"
    Alerted: { is: minecraft block list }
    blocks: => @Alerted:list each ({ stone: ore-gold sand: glass dirt: ore-coal } select @.. else @..)
    placing: => @blocks at @position
    Notifying: => @. with @world
  }
  player: uid-06b6-4631-166a-b013
  world: uid-b1d8-d170-a3d9-e693
}

{ UID: uid-a70f-cd0b-c257-a901
  is: editable minecraft spell
  title: "Cyrus Spell to build Pyramids"
  Rules: uid-320d-55ec-ca58-8f71 uid-77a8-38ec-e0bc-b5f1 uid-df7c-2043-f410-724f uid-4506-0c41-ac8e-4084 uid-f363-ddd9-4da2-4c73
  world: uid-b1d8-d170-a3d9-e693
  player: uid-06b6-4631-166a-b013
  shape: pyramid
  starting: false
  width: 7
  height: 30
  taper: 1
  material: stone-double-slab glass brick stone
}

{ UID: uid-320d-55ec-ca58-8f71
  is: editable minecraft spell rule
  when: "pyramid constructor filled in, set up to build"
  shape: pyramid
  player: { position: * }
  starting: true => false
  position: => @player:position
  h: => 0
  w: => @width
  i: => 0
  m: => @material:0
}

{ UID: uid-77a8-38ec-e0bc-b5f1
  is: editable minecraft spell rule
  when: "run, set scanning box and notify world"
  w: *
  place-position: => @position + (( 8 - (@w / 2)) @h (8 - (@w / 2)))
  scan-position: => @place-position + ( 0 3 0 )
  scanning: => box ( @w 1 @w ) at @scan-position
  Notifying: => @. with @world
}

{ UID: uid-df7c-2043-f410-724f
  is: editable minecraft spell rule
  when: "scan blocks notified, save"
  Alerted: { is: minecraft block list }
  scan-blocks: # => @Alerted
}

{ UID: uid-4506-0c41-ac8e-4084
  is: editable minecraft spell rule
  when: "no slab there, place it"
  scan-blocks: { position: @scan-position list: ((( !@m ))) }
  placing: => @m box ( @w 4 @w ) at @place-position
}

{ UID: uid-f363-ddd9-4da2-4c73
  is: editable minecraft spell rule
  when: "slab there, increase height and decrease width at current position"
  scan-blocks: { position: @scan-position list: not ((( !@m ))) }
  placing: => #
  h: <= @height => @h + 3
  w: > 0 => if (@h <= @height) then (@w - (2 * @taper)) else 0
  i: => @i + 1
  m: => @material select (@i % (count @material))
}

