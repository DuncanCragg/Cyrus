{ UID: uid-b1d8-d170-a3d9-e693
  Class: net.minecraft.src.CyrusWorld
  is: queryable updatable 3d minecraft world
}

{ UID: uid-06b6-4631-166a-b013
  Class: net.minecraft.src.CyrusUser
  is: 3d minecraft player
}

{ UID: uid-fb97-b02c-e69b-90dd
  is: editable minecraft spell list
  list: uid-a70f-cd0b-c257-a901
}

{ UID: uid-a70f-cd0b-c257-a901
  is: editable minecraft spell
  title: "Sample Cyrus Spell"
  Rules: uid-77a8-38ec-e0bc-b5f1 uid-df7c-2043-f410-724f uid-4506-0c41-ac8e-4084 uid-f363-ddd9-4da2-4c73
  world: uid-b1d8-d170-a3d9-e693
  player: uid-06b6-4631-166a-b013
  h: 0
  w: 0
  m: stone
}

{ UID: uid-77a8-38ec-e0bc-b5f1
  is: editable minecraft spell rule
  when: "player location set, set scanning box and notify world"
  player: { location: * }
  current-place: => @player:location + (( 8 - (@w / 2)) @h (8 - (@w / 2)))
  current-scan: => @current-place + ( 0 3 0 )
  scanning: => box ( @w 1 @w ) at @current-scan
  Notifying: => @. with @world
}

{ UID: uid-df7c-2043-f410-724f
  is: editable minecraft spell rule
  when: "world-view notified, save"
  Alerted: { is: minecraft world-view }
  world-view: # => @Alerted
}

{ UID: uid-4506-0c41-ac8e-4084
  is: editable minecraft spell rule
  when: "no slab there, place it"
  world-view: { at: @current-scan scan-view: ((( !@m ))) }
  placing: => @m box ( @w 4 @w ) at @current-place
}

{ UID: uid-f363-ddd9-4da2-4c73
  is: editable minecraft spell rule
  when: "slab there, increase height and decrease width at current location"
  world-view: { at: @current-scan scan-view: not ((( !@m ))) }
  h:     => @h + 3
  w: > 0 => @w - 2
}

