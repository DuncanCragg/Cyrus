{ UID: uid-f862-56af-d46d-c440
  Class: cyrus.types.PresenceTracker
  is: place 3d mesh editable
  title: "Your Room"
  sub-items:
    { item: uid-20ce-d0b1-c717-3dbb position: 0 1 0 }
    { item: uid-e30d-92d3-3de9-f898 position: 0 0 -2 }
    { item: uid-a07e-434c-b8e7-5b40 position: 3 3 3 }
    { item: uid-f6c8-2d51-8060-a52a position: 0 1 -11 }
    { item: http://10.0.2.2:8082/o/uid-c058-2db1-0b26-8f48.json position: 4 0 -7 }
    { item: http://10.0.2.2:8082/o/uid-c058-2db1-0b26-8f48.json position: -4 0 -7 }
    { item: http://10.0.2.2:8082/o/uid-c058-2db1-0b26-8f48.json position: 0 4 -7 }
    { item: http://10.0.2.2:8081/o/uid-1e51-f298-5f55-6caa.json position: 0 0 -30 }
  vertices: ( 20 -1 -20 ) ( 20 -1 20 ) ( -20 -1 20 ) ( -20 -1 -20 ) ( 20 20 -20 ) ( 20 20 20 ) ( -20 20 20 ) ( -20 20 -20 )
  texturepoints: ( 0 0 ) ( 5 0 ) ( 5 5 ) ( 0 5 )
  normals: ( 1 0 0 ) ( -1 0 0 ) ( 0 1 0 ) ( 0 -1 0 ) ( 0 0 1 ) ( 0 0 -1 )
  faces: ( 5/1/5 4/3/5 1/2/5 ) ( 5/1/5 8/4/5 4/3/5 ) ( 3/1/1 8/3/1 7/2/1 ) ( 3/1/1 4/4/1 8/3/1 ) ( 2/1/6 3/4/6 6/2/6 ) ( 6/2/6 3/4/6 7/3/6 )
         ( 1/1/2 2/4/2 5/2/2 ) ( 5/2/2 2/4/2 6/3/2 ) ( 5/1/4 6/4/4 8/2/4 ) ( 8/2/4 6/4/4 7/3/4 ) ( 1/1/3 3/3/3 2/2/3 ) ( 1/1/3 4/4/3 3/3/3 )
  textures: http://www.textures123.com/free-texture/sand/sand-texture4.jpg
  vertex-shader: http://10.0.2.2:8081/o/uid-ff5d-1ef4-cfa5-5f92.json
  fragment-shader: http://10.0.2.2:8081/o/uid-1ff8-59e9-6dac-9b56.json
}

{ UID: uid-e30d-92d3-3de9-f898
  Rules: {
    is: 3d notice rule editable
    when: "you edit this rule you can see the effect!"
  }
  is: 3d notice editable
  title: Cyrus example
  text: "Edit me: multitouch tap"
  light: 1 1 0
}

{ UID: uid-b553-d845-44d6-48ac
  Class: cyrus.types.Time
}

{ UID: uid-a07e-434c-b8e7-5b40
  Rules: uid-e31f-88ef-6c62-8440 uid-83a6-53ef-9f44-43bd uid-101b-2c33-9fd6-96a1
  is: 3d cuboid editable
  title: bot
  rotation: 45 45 45
  scale: 0.3 0.3 0.3
  light: 0.0 0.0 1
  within: uid-f862-56af-d46d-c440
  position: 0 0 -3
  time: uid-b553-d845-44d6-48ac
  next-time: 0
}

{ UID: uid-e31f-88ef-6c62-8440
  is:  3d cuboid editable rule
  when: "user present, set target user to first one"
  target-user: # => @=within:sub-items:item select 0
  within: {
    present: > 0
    sub-items: { item: { is: user } }
  }
}

{ UID: uid-83a6-53ef-9f44-43bd
  is:  3d cuboid editable rule
  when: "user position changes, set target position to just in front"
  target-user: { position: * }
  target-position: => @target-user:position + ( 2 1 -3 )
}

{ UID: uid-101b-2c33-9fd6-96a1
  is:  3d cuboid editable rule
  when: "time passes, set position to half way to target"
  target-position: ( number number number )
  position: within 0.5 of @target-position !=> ( @. + @target-position ) / 2
  time: { timestamp: > @next-time }
  next-time: => @time:timestamp + 1000
}

{ UID: uid-20ce-d0b1-c717-3dbb
  Rules: uid-e6e9-df58-ddab-6eb9
  is: 3d cuboid editable
  title: Light
  rotation: 0 45 0
  scale: 0.3 0.5 0.3
  light: 2 2 2
  v: 0
}

{ UID: uid-e6e9-df58-ddab-6eb9
  is: 3d rule editable
  when: "swiped, change light"
  Alerted: { is: swipe Version: > @v }
  v: => @Alerted:Version
  light: * ( => clamp 0 10 ( @. + ( 10 * @Alerted:dx ) ) ) ( => clamp 0 10 ( @. + ( 10 * @Alerted:dy ) ) )
}

{ UID: uid-f6c8-2d51-8060-a52a
  Rules: uid-8f4e-551c-858b-d345 uid-fb3e-02d3-70bb-916f uid-536b-de0b-41db-41d1 uid-fbd9-4a4d-c3c7-c31f
         uid-acc3-1f94-1458-ab18 uid-ab7f-889e-edb9-6bab uid-a9ff-4a17-9772-3859 uid-aad5-7236-b521-4780 uid-0b6a-659e-3561-05b2
         uid-4899-0de2-1bb5-58ba uid-3558-f24b-c0c4-d32b
  is: 3d notice editable
  title: "Maths Game"
  text: "Light up this fraction:" - -
  rotation: 0 0 0
  scale: 1 1 1
  light: 0.5 0.5 1
  sub-items: ( -1.2 -1.8 ) ( -0.4 -1.8 ) ( 0.4 -1.8 ) ( 1.2 -1.8 )
               ( -1.2 -1.0 ) ( -0.4 -1.0 ) ( 0.4 -1.0 ) ( 1.2 -1.0 )
               ( -1.2 -0.2 ) ( -0.4 -0.2 ) ( 0.4 -0.2 ) ( 1.2 -0.2 )
  numerator: 2
  status: creating
  lit: 0
  v: 0
}

{ UID: uid-8f4e-551c-858b-d345
  is: 3d rule editable
  when: "starting, create lights"
  status: creating => waiting
  sub-items: ( number number ) =>
      { item:
        { UID: new
          Rules: uid-1b54-d28a-71dc-5827 uid-7b1a-4b1d-1424-2818
          is: 3d cuboid editable
          title: "Game Cube"
          rotation: 0 0 0
          scale: 0.36 0.36 0.36
          light: 0 0.4 0.7
        }
        position: @.0 @.1 -0.5
      }
  denominator: => count @sub-items
}

{ UID: uid-fb3e-02d3-70bb-916f
  is: 3d rule editable
  when: "swiped right, set to checking"
  Alerted: { Version: > @v is: swipe dx: > 0 }
  v: => @Alerted:Version
  status: waiting => checking
}

{ UID: uid-536b-de0b-41db-41d1
  is: 3d rule editable
  when: "swiped left after sorry, set to waiting with old challenge"
  Alerted: { is: swipe dx: < 0 }
  status: sorry => waiting
}

{ UID: uid-fbd9-4a4d-c3c7-c31f
  is: 3d rule editable
  when: "swiped left after good, set to waiting and create random numerator"
  Alerted: { is: swipe dx: < 0 }
  status: good => waiting
  numerator: => random 0 @denominator
}

{ UID: uid-acc3-1f94-1458-ab18
  is: 3d rule editable
  when: "waiting, halve fraction if possible"
  status: waiting
  numerator:   divisible-by 2 => @. / 2
  denominator: divisible-by 2 => @. / 2
}

{ UID: uid-ab7f-889e-edb9-6bab
  is: 3d rule editable
  when: "waiting, third fraction if possible"
  status: waiting
  numerator:   divisible-by 3 => @. / 3
  denominator: divisible-by 3 => @. / 3
}

{ UID: uid-a9ff-4a17-9772-3859
  is: 3d rule editable
  when: "waiting, set text to challenge"
  status: waiting
  text: * ( => ( format %s/%s @numerator @denominator ) ) ( => "Swipe right when done ->" )
  light: ( => 0.5 ) ( => 0.5 ) ( => 1 )
}

{ UID: uid-aad5-7236-b521-4780
  is: 3d rule editable
  when: "checking, set to initial zero and recalc numerator and denominator"
  status: checking
  lit: => 0
  numerator: => ( @numerator / @denominator ) * ( count @sub-items )
  denominator: => count @sub-items
}

{ UID: uid-0b6a-659e-3561-05b2
  is: 3d rule editable
  when: "checking, count lit up lights"
  status: checking
  sub-items: { item: { light: * 1 1 } }
  lit: => count @=sub-items
}

{ UID: uid-4899-0de2-1bb5-58ba
  is: 3d rule editable
  when: "checking, if incorrect, set text"
  status: checking => sorry
  lit: !@numerator
  text: * * ( => "Sorry.. Swipe left <-" )
  light: ( => 1 ) ( => 0.5 ) ( => 0.5 )
}

{ UID: uid-3558-f24b-c0c4-d32b
  is: 3d rule editable
  when: "checking, if correct, set text"
  status: checking => good
  lit: @numerator
  text: * * ( => "Good! Swipe left <-" )
  light: ( => 0.5 ) ( => 1 ) ( => 0.5 )
}

{ UID: uid-1b54-d28a-71dc-5827
  is: 3d rule editable
  when: "swiped down, turn on light"
  Alerted: { is: swipe dy: > 0 }
  light: * ( => 1 ) ( => 1 )
}

{ UID: uid-7b1a-4b1d-1424-2818
  is: 3d rule editable
  when: "swiped up, turn off light"
  Alerted: { is: swipe dy: < 0 }
  light: * ( => 0.4 ) ( => 0.7 )
}

{ UID: uid-d9e4-4f7c-d9fe-9e7c
  Rules: uid-58cb-cc31-1769-c2c4 uid-3395-9f29-80d0-a8ac
  is: gui editable
  title: "Local Calculator GUI"
  view: {
    #panel:
      { #f: { input: chooser label: Function range: { prod: Multiply sum: Add diff: Subtract div: Divide } } }
      { #a: { input: textfield label: A value: 0 } }
      { #b: { input: textfield label: B value: 0 } }
   #result: { #r: 0 }
  }
}

{ UID: uid-58cb-cc31-1769-c2c4
  is: gui rule editable
  when: "form alerted, save it"
  Alerted: { is: form }
  form: => @Alerted
}

{ UID: uid-3395-9f29-80d0-a8ac
  is: gui rule editable
  when: "form changes, do calculation"
  view: {
    #panel:
      { #f: { value: => @form:form:f } }
      { #a: { value: => @form:form:a } }
      { #b: { value: => @form:form:b } }
    #result: { #r: => @form:form:a
                    ( { prod: * sum: + diff: - div: / } select @form:form:f )
                      @form:form:b
    }
  }
}

